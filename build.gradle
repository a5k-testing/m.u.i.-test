/*
 * Copyright (C) 2018 ale5000
 * SPDX-License-Identifer: GPL-3.0-or-later
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version, w/ zip exception.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.gradle.build-scan' version '3.7'
    id 'base'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlwaysIf(System.getenv('CI') != null)
}

def isWindows() {
    return System.properties['os.name'].toLowerCase(Locale.ROOT).contains('windows')
}

def is64BitArch() {
    return System.properties['os.arch'].contains('64')
}

def getBuildScriptName() {
    if(isWindows()) {
        return './build-win.bat'
    } else {
        return './build.sh'
    }
}

task configure() {
    buildDir = "${projectDir}/output"
    println 'OS: '+System.properties['os.name']
    println '64 bit: '+is64BitArch()
}

task cleanAll(type: Delete) {
    delete buildDir, "${projectDir}/cache"
}
clean.finalizedBy cleanAll

task buildOta(group: '- OTA build', description: 'Build the flashable OTA zip.') {
    doLast {
        println 'Building the flashable OTA zip with Gradle...'
        exec {
            executable = getBuildScriptName()
            args 'Gradle'
        }
    }
}

task buildOtaOSS(group: '- OTA build', description: 'Build the flashable OTA zip (open-source components only).') {
    doLast {
        println 'Building the flashable OTA zip (open-source components only) with Gradle...'
        exec {
            executable = getBuildScriptName()
            args 'Gradle'
            environment OPENSOURCE_ONLY: "1"
        }
    }
}
build.finalizedBy buildOtaOSS

task test(dependsOn: build) {
    // Dummy task
}

task install(dependsOn: build) {
    // Dummy task
}

wrapper {
    gradleVersion = '5.6.4'
    distributionType = Wrapper.DistributionType.ALL
    distributionSha256Sum = 'abc10bcedb58806e8654210f96031db541bcd2d6fc3161e81cb0572d6a15e821'
}
